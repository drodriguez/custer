env = Environment()

if env['PLATFORM'] == 'darwin': # Mac OS X, incluimos rutas de MacPorts
	env.PrependENVPath('PATH', "/opt/local/bin")
	env.Append(CPPPATH = "/opt/local/include")
	env.Append(LIBPATH = "/opt/local/lib")
	env.Append(CCFLAGS = '-g')
elif env['PLATFORM'] == 'win32':
	env.Append(CPPPATH = "c:\\programacion\\boost\\boost_1_35_0")
	env.Append(LIBPATH = "c:\\programacion\\boost\\boost_1_35_0\\lib")
	env.Append(CCFLAGS = '/MDd /Zi /EHsc')
	env.Append(CPPDEFINES = ['WIN32','BOOST_ALL_NO_LIB', 'BOOST_ALL_DYN_LINK'])
	env.Append(LINKFLAGS = '/DEBUG /SUBSYSTEM:CONSOLE')
else:
	env.Append(CCFLAGS = '-g')

if env.WhereIs('ragel'):
	print "Ragel encontrado"
	ragel = Builder(action = 'ragel -C -o ${TARGET.abspath} ${SOURCE.file}', chdir=1)
	env.Append(BUILDERS = {'Ragel' : ragel})
	
	parser = env.Ragel('http/parser.c', ['http/parser.rl', 'http/common.rl'])
	env.NoClean(parser)

sources = Split("""
	main.cpp
	CusterServer.cpp
	ListenEventHandler.cpp
	ClientEventHandler.cpp
	utils.cpp
	Dispatcher.cpp
	HttpRequest.cpp
	HttpResponse.cpp
	DirectorySender.cpp
	http/parser.c
""")

includes = Split("""
	custer.h
	const.h
	platform.h
	utils.h
	ClientEventHandler.h
	CusterServer.h
	DirectorySender.h
	Dispatcher.h
	EventHandler.h
	HttpRequest.h
	HttpResponse.h
	IDispatcher.h
	ListenEventHandler.h
	http/parser.h
""")

if env['PLATFORM'] == 'win32':
	sources.append("SelectDispatcher.cpp")
	includes.append("SelectDispatcher.h")
	libs = Split("""
		boost_system-vc80-mt-gd-1_35
		boost_program_options-vc80-mt-gd-1_35
		boost_filesystem-vc80-mt-gd-1_35
		boost_date_time-vc80-mt-gd-1_35
		ws2_32
	""")
elif env['PLATFORM'] == 'darwin':
	# sources.append("KQueueDispatcher.cpp")
	sources.append("SelectDispatcher.cpp")
	libs = Split("""
		boost_program_options
		boost_filesystem
	""")
# Que pasa con Linux y BSD? Los dos son 'posix'

objects = env.Object(sources)
program = env.Program('custer', objects, LIBS=libs)

#env.MSVSProject(
#	target = 'custer' + env['MSVSPROJECTSUFFIX'],
#	srcs = sources,
#	incs = includes,
#	buildtarget = program,
#	variant = 'Debug')
