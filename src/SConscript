env = Environment()

if env['PLATFORM'] == 'darwin': # Mac OS X, incluimos rutas de MacPorts
	env.PrependENVPath('PATH', "/opt/local/bin")
	env.Append(CPPPATH = "/opt/local/include")
	env.Append(LIBPATH = "/opt/local/lib")
	env.Append(CCFLAGS = '-g -Wall')
	# env.Append(CCFLAGS = '-O3')
elif env['PLATFORM'] == 'win32':
	#
	# Las dos lineas a continuacion se deberian modificar para cada sistema
	# con las rutas al lugar de instalacion de Boost.
	#
	env.Append(CPPPATH = "c:\\programacion\\boost\\boost_1_35_0")
	env.Append(LIBPATH = "c:\\programacion\\boost\\boost_1_35_0\\lib")
	env.Append(CCFLAGS = '/MDd /Zi /EHsc')
	env.Append(CPPDEFINES = ['WIN32','BOOST_ALL_NO_LIB', 'BOOST_ALL_DYN_LINK'])
	env.Append(LINKFLAGS = '/DEBUG /SUBSYSTEM:CONSOLE')
else:
	env.Append(CCFLAGS = '-g -Wall')

if env.WhereIs('ragel'):
	print "Ragel encontrado"
	ragel = Builder(action = 'ragel -C -o ${TARGET.abspath} ${SOURCE.file}', chdir=1)
	env.Append(BUILDERS = {'Ragel' : ragel})
	
	parser = env.Ragel('http/parser.c', ['http/parser.rl', 'http/common.rl'])
	env.NoClean(parser)

sources = Split("""
	main.cpp
	CusterServer.cpp
	ListenEventHandler.cpp
	parser_callbacks.cpp
	ClientEventHandler.cpp
	utils.cpp
	Dispatcher.cpp
	HttpRequest.cpp
	HttpResponse.cpp
	DirectorySender.cpp
	http/parser.c
""")

if env['PLATFORM'] == 'win32':
	sources.append("SelectDispatcher.cpp")
	#
	# Los nombres de las bibliotecas Boost cambian con cada version. Se
	# deberan modificar por aquellos de la version que utilicemos.
	#
	libs = Split("""
		boost_system-vc80-mt-gd-1_35
		boost_program_options-vc80-mt-gd-1_35
		boost_filesystem-vc80-mt-gd-1_35
		boost_date_time-vc80-mt-gd-1_35
		ws2_32
	""")
elif env['PLATFORM'] == 'darwin':
	sources.append("KQueueDispatcher.cpp")
	libs = Split("""
		boost_program_options
		boost_filesystem
	""")
# Que pasa con Linux y BSD? Los dos son 'posix'

objects = env.Object(sources)
program = env.Program('custer', objects, LIBS=libs)
