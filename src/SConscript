env = Environment()


if env['PLATFORM'] == 'darwin': # Mac OS X, incluimos rutas de MacPorts
	env.PrependENVPath('PATH', "/opt/local/bin")
	env.Append(CPPPATH = "/opt/local/include")
	env.Append(LIBPATH = "/opt/local/lib")

env.Append(CCFLAGS = '-g')

if env.WhereIs('ragel'):
	print "Ragel encontrado"
	ragel = Builder(action = 'ragel -C -o ${TARGET.abspath} ${SOURCE.file}', chdir=1)
	env.Append(BUILDERS = {'Ragel' : ragel})
	
	parser = env.Ragel('http/parser.c', ['http/parser.rl', 'http/common.rl'])
	env.NoClean(parser)

sources = Split("""
	main.cpp
	CusterServer.cpp
	ListenEventHandler.cpp
	parser_callbacks.cpp
	ClientEventHandler.cpp
	utils.cpp
	Dispatcher.cpp
	KQueueDispatcher.cpp
	HttpRequest.cpp
	HttpResponse.cpp
	DirectorySender.cpp
	http/parser.c
""")

includes = Split("""
	custer.h
	const.h
	platform.h
	utils.h
	parser_callbacks.h
	ClientEventHandler.h
	CusterServer.h
	DirectorySender.h
	Dispatcher.h
	EventHandler.h
	HttpRequest.h
	HttpResponse.h
	IDispatcher.h
	KQueueDispatcher.h
	ListenEventHandler.h
	http/parser.h
""")

libs = Split("""
	boost_program_options
	boost_filesystem
""")

objects = env.Object(sources)
program = env.Program('custer', objects, LIBS=libs)

# env.MSVSProject(
#	target = 'custer' + env['MSVSPROJECTSUFFIX'],
#	srcs = sources,
#	incs = includes,
#	buildtarget = program,
#	varian = 'Release')
